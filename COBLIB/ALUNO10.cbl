       IDENTIFICATION DIVISION.
	   PROGRAM-ID.
	       ALUNO10.
	   AUTHOR.
	       PHILIPP.
	  ***************************************
	  *    IMPRIMIR RELATORIO DE CLIENTES   *
	  ***************************************
	  *
	   ENVIRONMENT DIVISION.
	   CONFIGURATION SECTION.
	   SPECIAL-NAMES.
	       DECIMAL-POINT IS COMMA.
	   INPUT-OUTPUT SECTION.
	   FILE-CONTROL.
	       SELECT ARQ-CLIENTES ASSIGN TO CLIENTES
		       FILE STATUS IS WK-FS-CLIENTES.
		   SELECT ARQ-RELATO ASSIGN TO RELATO.
	  *
	   DATA DIVISION.
	   FILE SECTION.
	   FD   ARQ-CLIENTES
	        RECORDING MODE IS F.
	   01  REG-CLIENTES.
	       05 FD-CHAVE-CLIENTES.
	           10 FD-AGENCIA-CLIENTES     PIC X(4).
		       10 FD-CONTA-CLIENTES       PIC 9(5).
		   05 FD-NOME-CLIENTES        PIC A(20).
		   05 FD-SALDO-CLIENTES       PIC 9(6)V99.
	   FD   ARQ-RELATO
	        RECORDING MODE IS F.
	   01  REG-RELATO                 PIC X(50).
	   WORKING-STORAGE SECTION.
	   01  WK-CABEC1.
	       05 FILLER       PIC X(04)    VALUE SPACES.
		   05 FILLER       PIC X(21)    VALUE 'RELATORIO DE CLIENTES'.
		   05 FILLER       PIC X(08)    VALUE SPACES.
		   05 FILLER       PIC X(04)    VALUE 'PAG '.
		   05 WK-NUMPAG    PIC 999      VALUE ZEROS.
	   01  WK-CABEC2.
	       05 FILLER       PIC X(09)    VALUE SPACES.
		   05 FILLER       PIC X(10)    VALUE 'AGENCIA : '.
		   05 WK-AG-CABEC2 PIC X(04)    VALUE SPACES.
	   01  WK-CABEC3.
	       05 FILLER       PIC X(05)    VALUE 'CONTA'.
		   05 FILLER       PIC X(02)    VALUE SPACES.         
		   05 FILLER       PIC X(04)    VALUE 'NOME'.
		   05 FILLER       PIC X(22)    VALUE SPACES.
		   05 FILLER       PIC X(05)    VALUE 'SALDO'.
	   01 WK-DETALHE.
	       05 WK-CONTA-DET PIC 9(05)      VALUE ZEROS.
		   05 FILLER       PIC X(02)      VALUE SPACES.
		   05 WK-NOME-DET  PIC A(20)      VALUE SPACES.
		   05 FILLER       PIC X(01)      VALUE SPACES.
		   05 WK-SALDO-DET PIC ZZZ.ZZ9,99 VALUE ZEROS.
	   01  WK-RODAPE. 
	       05 FILLER        PIC A(17)         VALUE 'TOTAL DA AGENCIA '.
		   05 WK-AG-RODAPE  PIC X(04)         VALUE SPACES.
		   05 FILLER        PIC X(04)         VALUE ' -> '.
		   05 WK-TOT-RODAPE PIC ZZ.ZZZ.ZZ9,99 VALUE ZEROS.
	   77  WK-FS-CLIENTES  PIC XX           VALUE SPACES.
	   77  WK-AG-CORRENTE  PIC X(04)        VALUE SPACES.
	   77  WK-TOT-AGENCIA  PIC 9(8)V99      VALUE ZEROS.
	   77  WK-CONTALIN     PIC 9            VALUE ZEROS.
	  *
	   PROCEDURE DIVISION.
	   000-PRINCIPAL SECTION.
	   001-PRINCIPAL.
	       PERFORM 101-INICIAR.
		   PERFORM 201-PROCESSAR UNTIL WK-FS-CLIENTES = '10'.
		   PERFORM 901-FINALIZAR.
		   STOP RUN.
	  ***************************************	   
	   100-INICIAR SECTION.
	   101-INICIAR.
	       OPEN INPUT ARQ-CLIENTES.
		   EVALUATE WK-FS-CLIENTES
		       WHEN '00'
			       PERFORM 301-LER-CLIENTES
				   MOVE FD-AGENCIA-CLIENTES  TO WK-AG-CORRENTE
			   WHEN '35'
				   DISPLAY 'ARQUIVO CLIENTES NAO ENCONTRADO'
				   MOVE 12 TO RETURN-CODE
				   STOP RUN
			   WHEN OTHER
			       DISPLAY 'ERRO ' WK-FS-CLIENTES
				           ' NO COMANDO OPEN CLIENTES'
				   MOVE 12 TO RETURN-CODE
				   STOP RUN
		   END-EVALUATE.
		   OPEN OUTPUT ARQ-RELATO.
	  ***************************************
	   200-PROCESSAR SECTION.
	   201-PROCESSAR.
	       PERFORM 202-IMPRIME-CLIENTES.
		   PERFORM 301-LER-CLIENTES.
	   202-IMPRIME-CLIENTES.
	       IF FD-AGENCIA-CLIENTES NOT = WK-AG-CORRENTE
		       PERFORM 203-TOTALIZA
		   END-IF.
		   IF WK-CONTALIN = 0 OR WK-CONTALIN >= 5
		       PERFORM 204-CABECALHO
		   END-IF.
		   PERFORM 205-IMPRIME.
	   203-TOTALIZA.
	       MOVE WK-AG-CORRENTE  TO WK-AG-RODAPE.
		   MOVE WK-TOT-AGENCIA  TO WK-TOT-RODAPE.
		   WRITE REG-RELATO FROM WK-RODAPE AFTER 2 LINES.
		   MOVE 0  TO WK-TOT-AGENCIA WK-CONTALIN.
	       MOVE FD-AGENCIA-CLIENTES  TO WK-AG-CORRENTE.
	   204-CABECALHO.
	       ADD 1 TO WK-NUMPAG.
		   MOVE WK-AG-CORRENTE  TO WK-AG-CABEC2.
		   WRITE REG-RELATO FROM WK-CABEC1 AFTER PAGE.
		   WRITE REG-RELATO FROM WK-CABEC2.
		   WRITE REG-RELATO FROM WK-CABEC3 AFTER 2 LINES.
		   MOVE 0  TO WK-CONTALIN.
	   205-IMPRIME.
	       MOVE FD-CONTA-CLIENTES  TO WK-CONTA-DET.
		   MOVE FD-NOME-CLIENTES   TO WK-NOME-DET.
		   MOVE FD-SALDO-CLIENTES  TO WK-SALDO-DET.
		   WRITE REG-RELATO FROM WK-DETALHE.
		   ADD 1                   TO WK-CONTALIN.
		   ADD FD-SALDO-CLIENTES   TO WK-TOT-AGENCIA.
	  ***************************************
	   300-LER-CLIENTES SECTION.
	   301-LER-CLIENTES.
	       READ ARQ-CLIENTES.
		   EVALUATE WK-FS-CLIENTES
		       WHEN '00'
			       CONTINUE
			   WHEN '10'
			       CONTINUE
			   WHEN OTHER
			       DISPLAY 'ERRO ' WK-FS-CLIENTES
				           ' NO COMANDO READ CLIENTES'
				   MOVE 12 TO RETURN-CODE
				   STOP RUN
           END-EVALUATE.
      ***************************************
       900-FINALIZAR SECTION.
       901-FINALIZAR.
	       PERFORM 203-TOTALIZA.
	       CLOSE ARQ-CLIENTES.
		   CLOSE ARQ-RELATO.